plugins {
	id "dev.architectury.loom" version "1.9-SNAPSHOT"
	id "maven-publish"
	id "me.shedaniel.unified-publishing" version "0.1.+"
}

// apply from: 'https://files.latmod.com/public/markdown-git-changelog.gradle'
// Commented out due to network issues - handle changelog manually

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '21'

def ENV = System.getenv()
version = "${mod_version}-build.${ENV.GITHUB_RUN_NUMBER ?: '9999'}"
archivesBaseName = "${project.archives_base_name}-fabric"
group = project.maven_group

loom {
	silentMojangMappingsLicense()
}

repositories {
	// Local libs directory for mods with version string issues
	flatDir {
		dirs 'libs'
	}

	maven {
		url "https://maven.saps.dev/releases"
	}

	// the maven for the devOS repo, for create fabric
	// (and a bunch of libraries I'm assuming)
	maven { url "https://mvn.devos.one/snapshots/" }
	maven { url "https://mvn.devos.one/releases/" }

	maven { url = "https://maven.terraformersmc.com/releases/" } // Mod Menu
	maven { url = "https://maven.jamieswhiteshirt.com/libs-release" } // Reach Entity Attributes

	maven {
		url "https://maven.tterrag.com/"
		content {
			includeGroup "com.jozufozu.flywheel"
			includeGroup "com.tterrag.registrate_fabric"
		}
	}

	// jitpack?! why?!
	maven {
		url "https://jitpack.io/"
		content {
			includeGroup "com.github.LlamaLad7"
			includeGroup "com.github.llamalad7.mixinextras" // two different versions of mixinextras on the classpath?? WHY?!
			includeGroup "com.github.Chocohead"
			includeGroup "com.github.Draylar.omega-config"
		}
	}

	// "i play both sides, that way i always come out ahead"
	maven {
		url "https://api.modrinth.com/maven"
		content {
			includeGroup "maven.modrinth"
		}
	}
	maven {
		url "https://cursemaven.com"
		content {
			includeGroup "curse.maven"
		}
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
	mappings loom.officialMojangMappings()

	modApi("net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}")
	modApi("net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_api_version}")

	// Architectury API - cross-platform compatibility layer
	modApi("dev.architectury:architectury-fabric:9.2.14")

	// Rhino - JavaScript engine used by KubeJS
	modApi("dev.latvian.mods:rhino-fabric:2001.2.3-build.10")

	// KubeJS from maven.saps.dev
	modApi("dev.latvian.mods:kubejs-fabric:${rootProject.kubejs_version}")

	// Porting-Lib modules (same as Create uses) - provides FluidStack and other forge-like APIs
	modApi("io.github.fabricators_of_create.Porting-Lib:transfer:2.3.13+1.20.1")
	modApi("io.github.fabricators_of_create.Porting-Lib:base:2.3.13+1.20.1")

	// Create from local libs directory (copied from WOWID3/mods for proper compilation)
	modApi(name: "create-fabric-6.0.8.0+build.1734-mc1.20.1", ext: "jar")

	// Registrate - for ItemEntry support (compileOnly since Create includes it)
	modCompileOnly("com.tterrag.registrate_fabric:Registrate:1.3.79-MC1.20.1")

	modLocalRuntime modCompileOnly("curse.maven:jei-238222:4494409")
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

java {
	withSourcesJar()
}

jar {
	archiveClassifier.set "dev"
}

remapJar {
    archiveBaseName.set "${rootProject.archives_base_name}-fabric"
    archiveClassifier.set null
}

publishing {
	publications {
		mavenFabric(MavenPublication) {
			artifactId = rootProject.archivesBaseName
			from components.java
		}
	}

	repositories {
		if (ENV.SAPS_TOKEN) {
			maven {
				url "https://maven.saps.dev/minecraft"
				credentials {
					username = "latvian"
					password = "${ENV.SAPS_TOKEN}"
				}
			}
		}

		if (ENV.MAVEN_TOKEN) {
			maven {
				url "https://maven.latvian.dev/releases"
				credentials {
					username = "lat"
					password = "${ENV.MAVEN_TOKEN}"
				}
			}
		}
	}
}


unifiedPublishing {
	project {
		releaseType = "${ENV.RELEASE_TYPE ?: 'release'}"
		gameVersions = [
				rootProject.minecraft_version
		]
		gameLoaders = ["fabric"]
		displayName = "$mod_name Fabric $project.version"
		changelog = "See GitHub for full changelog"
		mainPublication remapJar

		relations {
			depends {
				curseforge = modrinth = "kubejs"
            }
            depends {
                curseforge = modrinth = "create-fabric"
            }
		}

		if (ENV.CURSEFORGE_KEY) {
			curseforge {
				token = ENV.CURSEFORGE_KEY
				id = project.curseforge_id
			}
		}

		if (ENV.MODRINTH_TOKEN) {
			modrinth {
				token = ENV.MODRINTH_TOKEN
				id = project.modrinth_id
				version = "$project.version+fabric"
			}
		}
	}
}
